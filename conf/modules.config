/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    publishDir = [
        path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]

    withName: FASTQC {
        ext.args = '--quiet'
    }

    withName: CUSTOM_DUMPSOFTWAREVERSIONS {
        publishDir = [
            path: { "${params.outdir}/pipeline_info" },
            mode: params.publish_dir_mode,
            pattern: '*_versions.yml'
        ]
    }

    withName: 'MULTIQC' {
        ext.args   = { params.multiqc_title ? "--title \"$params.multiqc_title\"" : '' }
        publishDir = [
            path: { "${params.outdir}/multiqc" },
            mode: params.publish_dir_mode,
            saveAs: {
                filename -> 
                    switch (filename) {
                        case 'versions.yml':
                            null
                            break
                        case ~/L\d+_multiqc_(report\.html|plots|data)/:
                            def lane = (filename =~ /L(\d+)_multiqc_(report\.html|plots|data)/)[0][1]
                            def new_filename = filename.replaceFirst(
                                "(?<prefix>.*)L${lane}_(?<suffix>multiqc_(report\\.html|plots|data).*)",
                                '${prefix}${suffix}')
                            "lanes/L${lane}/${new_filename}"
                            break
                        case ~/G-.+_multiqc_(report\.html|plots|data)/:
                            def group = (filename =~ /G-(.+)_multiqc_(report\.html|plots|data)/)[0][1]
                            def new_filename = filename.replaceFirst(
                                "(?<prefix>.*)G-${group}_(?<suffix>multiqc_(report\\.html|plots|data).*)",
                                '${prefix}${suffix}')
                            "groups/${group}/${new_filename}"
                            break
                        case ~/D-.+_multiqc_(report\.html|plots|data)/:
                            def rundir = (filename =~ /D-(.+)_multiqc_(report\.html|plots|data)/)[0][1]
                            def new_filename = filename.replaceFirst(
                                "(?<prefix>.*)D-${rundir}_(?<suffix>multiqc_(report\\.html|plots|data).*)",
                                '${prefix}${suffix}')
                            "rundirs/${rundir}/${new_filename}"
                            break
                        default:
                            filename
                }
            }
        ]
    }

}
